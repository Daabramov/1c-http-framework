
&НаКлиенте
Процедура СобратьСхему(Команда)
	СобратьСхемуНаСервере();
КонецПроцедуры

&НаСервере
Процедура СобратьСхемуНаСервере()
	Генератор = ГенераторOAS.НовыйГенераторСхемы();
	Генератор
	.ДобавитьИнфо("Тестовая схема", "1.0")
	.ДобавитьКонтакт("daabramov", , "i@dima-abramov.ru")
	.ДобавитьЛицензию("MIT")
	.ДобавитьСервер("http://localhost/api");
	
	ПараметрыШаблонов = Демо_КонтроллерСервиса.ПараметрыШаблоновURL();
	ПараметрыМетодов = Демо_КонтроллерСервиса.ПараметрыМетодов();
	
	Для Каждого Шаблон Из Метаданные.HTTPСервисы.Демо_.ШаблоныURL Цикл
		
		Генератор.ДобавитьПуть(Шаблон.Шаблон, Шаблон.Синоним, Шаблон.Комментарий);
		
		ПараметрыПути = ПараметрыШаблонов.Получить(Шаблон);
		Если ПараметрыПути <> Неопределено Тогда
			Для Каждого Параметр Из ПараметрыПути Цикл
				Генератор.ДобавитьПараметр(Параметр.Имя, "path", Истина, ТипПараметраСхемыПоОписанию(Параметр, Генератор));
			КонецЦикла;
		КонецЕсли;
		
		Для Каждого Метод Из Шаблон.Методы Цикл
			
			Генератор.ДобавитьМетод(НРег(Строка(Метод.HTTPМетод)), Метод.Синоним, Метод.Комментарий);
			
			ПараметрыМетода = ПараметрыМетодов.Получить(Метод);
			Если ПараметрыМетода <> Неопределено Тогда
				Для Каждого Параметр Из ПараметрыМетода Цикл
					Генератор.ДобавитьПараметр(
						Параметр.Имя,
						"query",
						Параметр.Обязательный,
						ТипПараметраСхемыПоОписанию(Параметр, Генератор)
					);
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Реквизит1 = Генератор.ПолучитьСхему();
	
КонецПроцедуры

// Тип параметра схемы по описанию.
// 
// Параметры:
//  Параметр - см. СервисыОбщее.НовыйПараметрЗапроса
//  ГенераторСхемы - ОбработкаОбъект.ГенераторOAS
// 
// Возвращаемое значение:
&НаСервере
Функция ТипПараметраСхемыПоОписанию(Параметр, ГенераторСхемы)
	
	Результат = ГенераторСхемы.ДоступныеТипы[Врег(Параметр.Тип)];

	Если Параметр.ВозможныеЗначения.Количество() > 0 Тогда
		Результат = Новый Структура(Результат);
		Результат.Вставить("enum", Параметр.ВозможныеЗначения);
		Возврат Новый ФиксированнаяСтруктура(Результат);
	Иначе
		Возврат Результат;
	КонецЕсли;
			
КонецФункции

&НаСервере
Функция ПараметрыВПути(Шаблон)
	ПараметрыВПути = Новый Массив;
	СоставПути = СтрРазделить(Шаблон, "/", Ложь);

	Для Каждого Элемент Из СоставПути Цикл

		Если Лев(Элемент, 1) = "{" И Прав(Элемент, 1) = "}" Тогда
			ПараметрыВПути.Добавить(Сред(Элемент, 2, СтрДлина(Элемент) - 2));
		КонецЕсли;

	КонецЦикла;
	
	Возврат ПараметрыВПути;
	
КонецФункции
