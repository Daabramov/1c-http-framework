// @strict-types


#Область ПрограммныйИнтерфейс

// Параметры создания заявки.
// 
// Параметры:
//  Параметры - см. Документы.Демо_Заявка.НовыеДанные
// 
// Возвращаемое значение:
//  ДокументСсылка.Демо_Заявка
Функция СоздатьЗаявку(Параметры) Экспорт
	НачатьТранзакцию();
	Попытка
		Заявка = Документы.Демо_Заявка.СоздатьДокумент();
		Заявка.Заполнить(Параметры);
		
		Если Не Заявка.ПроверитьЗаполнение() Тогда
			ВызватьИсключение "Документ не прошел проверку заполнения";
		КонецЕсли;
		
		Заявка.Записать();
		ЗаявкаСсылка = Заявка.Ссылка;
		ТекстСообщения = Параметры.Сообщение; // Строка
		СоздатьСообщение(ЗаявкаСсылка, ТекстСообщения);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ЗаявкаСсылка;
КонецФункции

// Создать сообщение.
// 
// Параметры:
//  Заявка - ДокументСсылка.Демо_Заявка
//  ТекстСообщения - Строка
//  СообщениеОтвета - ДокументСсылка.Демо_Сообщение
// 
// Возвращаемое значение:
//  ДокументСсылка.Демо_Сообщение
Функция СоздатьСообщение(Заявка, ТекстСообщения, СообщениеОтвета = Неопределено) Экспорт
	
	СообщениеОбъект = Документы.Демо_Сообщение.СоздатьДокумент();
	СообщениеОбъект.Заполнить(Неопределено);
	СообщениеОбъект.Запрос = Заявка;
	СообщениеОбъект.ТелоСообщения = ТекстСообщения;
	СообщениеОбъект.СообщениеОтвета = СообщениеОтвета;
	СообщениеОбъект.Записать();
	
	Возврат СообщениеОбъект.Ссылка;
	
КонецФункции

#КонецОбласти
